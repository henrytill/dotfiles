# -*- mode: gdb-script; -*-

### https://justine.lol/cosmopolitan/windows-debugging.html

add-auto-load-safe-path ~/.config/gdb/gdbinit
set disassembly-flavor intel
set host-charset UTF-8
set target-charset UTF-8
set target-wide-charset UTF-8
set style enabled off
set complaints 0
set confirm off
set history save on
set history filename ~/.cache/gdb/history
set history size unlimited

# show source tui
define src
  layout src
  layout reg
end

# show assembly tui
define asm
  layout asm
  layout reg
end

# print backtrace w/ rbp
# fallback for when `bt` is broken
# we recommend -fno-omit-frame-pointer
# needed because ape won't use -funwind-tables
define et
  set $x = (void **)$rbp
  while $x
    x/2a $x
    set $x = (void **)$x[0]
  end
end

# https://triptico.com/blog/a-better-hex-dump-for-gdb.html
define hd
  dump binary memory dump.bin $arg0 ((void *)$arg0)+128
  eval "shell hd dump.bin; rm dump.bin"
end

python
import os

def setup_rust_paths():
    commit_hash = os.getenv('RUSTC_COMMIT_HASH')
    sysroot = os.getenv('RUSTC_SYSROOT')
    if commit_hash and sysroot:
        rust_path = f"/rustc/{commit_hash}"
        rust_src = f"{sysroot}/lib/rustlib/src/rust"
        gdb.execute(f"set substitute-path {rust_path} {rust_src}")

setup_rust_paths()
end
